/*
 * Copyright 2011-2013 Blender Foundation
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#include "stdosl.h"

float safe_divide(float a, float b)
{
  return (b != 0.0) ? a / b : 0.0;
}

vector safe_divide(vector a, vector b)
{
  return vector((b[0] != 0.0) ? a[0] / b[0] : 0.0,
                (b[1] != 0.0) ? a[1] / b[1] : 0.0,
                (b[2] != 0.0) ? a[2] / b[2] : 0.0);
}

vector project(vector v, vector v_proj)
{
  float lenSquared = dot(v_proj, v_proj);
  return (lenSquared != 0.0) ? (dot(v, v_proj) / lenSquared) * v_proj : vector(0.0);
}

vector snap(vector a, vector b)
{
  return vector(floor(safe_divide(a[0], b[0])) * b[0],
                floor(safe_divide(a[1], b[1])) * b[1],
                floor(safe_divide(a[2], b[2])) * b[2]);
}

shader node_vector_math(string type = "add",
                        vector A = vector(0.0, 0.0, 0.0),
                        vector B = vector(0.0, 0.0, 0.0),
                        float Scale = 1.0,
                        output float Value = 0.0,
                        output vector Vector = vector(0.0, 0.0, 0.0))
{
  if (type == "add") {
    Vector = A + B;
  }
  else if (type == "subtract") {
    Vector = A - B;
  }
  else if (type == "multiply") {
    Vector = A * B;
  }
  else if (type == "divide") {
    Vector = safe_divide(A, B);
  }
  else if (type == "cross_product") {
    Vector = cross(A, B);
  }
  else if (type == "project") {
    Vector = project(A, B);
  }
  else if (type == "reflect") {
    Vector = reflect(A, B);
  }
  else if (type == "dot_product") {
    Value = dot(A, B);
  }
  else if (type == "distance") {
    Value = distance(A, B);
  }
  else if (type == "length") {
    Value = length(A);
  }
  else if (type == "scale") {
    Vector = A * Scale;
  }
  else if (type == "normalize") {
    Vector = normalize(A);
  }
  else if (type == "snap") {
    Vector = snap(A, B);
  }
  else if (type == "floor") {
    Vector = floor(A);
  }
  else if (type == "ceil") {
    Vector = ceil(A);
  }
  else if (type == "modulo") {
    Vector = mod(A, B);
  }
  else if (type == "fraction") {
    Vector = A - floor(A);
  }
  else if (type == "absolute") {
    Vector = abs(A);
  }
  else if (type == "minimum") {
    Vector = min(A, B);
  }
  else if (type == "maximum") {
    Vector = max(A, B);
  }
  else {
    warning("%s", "Unknown vector math operator!");
  }
}
